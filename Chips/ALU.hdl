// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input? 
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
	// process X
	Mux16(a=x, b=false, sel=zx, out=processedZX);
	Not16(in=processedZX, out=negatedZX);
	Mux16(a=processedZX, b=negatedZX, sel=nx, out=processedNX);

	// process Y
	Mux16(a=y, b=false, sel=zy, out=processedZY);
	Not16(in=processedZY, out=negatedZY);
	Mux16(a=processedZY, b=negatedZY, sel=ny, out=processedNY);
	
	// Perform appropriate operation (and or addition)
	And16(a=processedNX, b=processedNY, out=bitwisedAndOutput);
	Add16(a=processedNX, b=processedNY, out=addedOutput);
	Mux16(a=bitwisedAndOutput, b=addedOutput, sel=f, out=determinedOpOutput);
	
	// Negate if necessary and output OUT. Also output NG here as we use twos complement, so if the most significant bit is 1, number must be negative.
	Not16(in=determinedOpOutput, out=negatedDeterminedOpOutput);
	Mux16(a=determinedOpOutput, b=negatedDeterminedOpOutput, sel=no, out[0..7]=outFirstHalf, out[8..15]=outSecondHalf, out[15]=ng, out=out);

	// Calculate zr by determining if there are any 1 bits (all 0 bits = must be 0), and output ZR.
	Or8Way(in=outFirstHalf, out=firstHalfOr8Way);
	Or8Way(in=outSecondHalf, out=secondHalfOr8Way);
	Or(a=firstHalfOr8Way, b=secondHalfOr8Way, out=any1s);	
	Not(in=any1s, out=zr);
}