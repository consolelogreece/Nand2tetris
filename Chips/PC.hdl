// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/3/a/PC.hdl
/**
 * A 16-bit counter.
 * if      reset(t): out(t+1) = 0
 * else if load(t):  out(t+1) = in(t)
 * else if inc(t):   out(t+1) = out(t) + 1
 * else              out(t+1) = out(t)
 */
CHIP PC {
    IN in[16],inc, load, reset;
    OUT out[16];
    
    PARTS:	
	// if incrementing, you increment the contents of the register, not the IN.
	Inc16(in=regout, out=incInTemp);
	
	// done in an order such that it obeys the if chain above. reset takes precedence, followed by load, and then increment.
	Mux16(a=regout, b=incInTemp, sel=inc, out=incIn);
	Mux16(a=incIn, b=in, sel=load, out=loadIn);
	Mux16(a=loadIn, b=false, sel=reset, out=rIn);
	Register(in=rIn, load=true, out=out, out=regout);	
}