// taken from http://nand2tetris-questions-and-answers-forum.52.s1.nabble.com/Pseudo-Random-Number-Generator-td4026059.html

class Random {
    static int seed;
    static int A;
    static int M;
    static int Q;
    static int R;

    function void setSeed(int newSeed) {
        let seed = newSeed;
        if(seed=0) {
           let seed=1;
        }
        let A=219;
        let M=32749;
        let Q=M/A;
        let R=Random.mod(M,A);
        return;
    }

    function int rand() {
        var int test;
        let test=(A*(Random.mod(seed,Q)))-(R*(seed/Q));
        if(test<0) {
           let seed=test+M;
        }
        else {
           let seed=test;
        }
        return seed;
    }

    function int randRange(int low, int high) {
       var int scale;
       let scale = (M / (high - low + 1));
       return (Random.rand() / scale) + low;
    }

    function int mod(int a, int b) {
        var int d;
        var int r;
        let d = Math.divide(a,b);
        let r = a - (b * d);
        return r;
    }
}
